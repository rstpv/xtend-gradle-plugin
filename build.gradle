buildscript {
	repositories {
        jcenter {
            url "http://jcenter.bintray.com/"
        }
	}
	dependencies {
		classpath 'org.xtend:xtend-gradle-plugin:0.4.14'
		classpath 'com.github.oehme.sobula:sobula:0.6.7'
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'com.github.oehme.sobula.bintray-release'

	group = "org.xtend"

	bintray.pkg.name = "$project.group:$project.name"

	contacts {
		"xtext@itemis.com" {
			moniker "The Xtext team"
			roles "owner"
			github "xtext"
		}
	}

	//TODO create bintray and central users
	bintray.user = "oehme"
	bintray.pkg.version.mavenCentralSync.user = "oehme"

	repositories {

        jcenter {
            url "http://jcenter.bintray.com/"
        }

  	}

	sourceCompatibility = 1.6
}

configure(subprojects.findAll{p-> p.name.contains('plugin')}){
	apply plugin: 'com.github.oehme.sobula.plugin-release'
	apply plugin: 'java-gradle-plugin'
	apply plugin: 'org.xtend.xtend'

	dependencies {
		compile ('org.eclipse.xtend:org.eclipse.xtend.lib:2.8.4') {
			exclude group: 'org.eclipse.emf'
		}
		compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore', version: '2.12.0'
		compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.common', version: '2.12.0'
		compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi', version: '2.12.0'
		compile group: 'org.eclipse.emf', name: 'org.eclipse.emf.codegen', version: '2.11.0'

	}
}
